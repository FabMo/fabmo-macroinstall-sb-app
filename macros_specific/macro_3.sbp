'Set XYZ Zero Locations -- MACRO #3 -- [C3] in OpenSBP Language // Desktop (using prox switches)
' ... written in OpenSBP; 3/1/2016; updated 11/10/16, 3/16/17 th **NOTE ISSUE WITH Z and VA call

 IF %(25)=1 THEN GOSUB UNIT_ERROR	'make sure we are in inches for doing zeroing, auto restore mm

 'Read Offset or Set to Default
    $current_cutter_Zoffset := .25
    $x_backoff := .5
    $y_backoff := .5

  'Local variables
   	&approach_speed_fast = 1.0
	&approach_speed_slow = 0.25
	&search_distance_x = 25.0
	&search_distance_y = -19.0
	&search_distance_z =  6.0

' Save the XY speed
	&saved_XYspeed = %(71)
	&saved_Zspeed = %(73)

' Make sure we're off the switches
	IF %(53) = 1 THEN GOSUB withdraw_all

' Zero All (allows shorter search because no extreme values)
    Z3

' Home the X-axis
	  VS, &approach_speed_fast
	GOSUB approach_x
	GOSUB withdraw_x
	  VS, &approach_speed_slow
	GOSUB approach_x
	GOSUB backoff_x
	  VA, 0,,,,,,0               ' Zero the Axis and Set Table Base Coordinate to 0

' Home the Y-axis
	  VS, &approach_speed_fast
	GOSUB approach_y
	GOSUB withdraw_y
	  VS, &approach_speed_slow 
	GOSUB approach_y
	GOSUB backoff_y
	  VA, ,0,,,,,,0               ' Zero the Axis and Set Table Base Coordinate to 0

'*Z axis ISSUE; see below 
'' Home the Z-axis
	  VS,, &approach_speed_fast
	GOSUB approach_z
	GOSUB withdraw_z
	  VS,, &approach_speed_slow 
	GOSUB approach_z
   '* The following is not the correct VA call; it works only for versions 1.51 and lower
      VA, ,,$current_cutter_Zoffset,,,,,,$current_cutter_Zoffset,, ' Locate the Axis and Set Table Base Coordinate to 0
	GOSUB backoff_z

' Restore speeds
	VS, &saved_XYspeed, &saved_Zspeed

END


' SUBROUTINES BELOW HERE =============================

approach_x:
   ON INPUT(3,1) RETURN
   MX, &search_distance_x
   ' FAIL if we get there
   GOTO failure
   END
   
approach_y:
   ON INPUT(3,1) RETURN
   MY, &search_distance_y
   ' FAIL if we get here also
   GOTO failure
   END
   
approach_z:
   ON INPUT(3,1) RETURN
   MZ, &search_distance_z
   ' FAIL if we get here also
   GOTO failure
   END

backoff_x:
     JX, %(1) + $x_backoff
   RETURN
backoff_y:
     JY, %(2) + $y_backoff
   RETURN
backoff_z:
     JZ, 1
   RETURN

withdraw_x:
     JX, %(1) + 0.5 
   RETURN
withdraw_y:
     JY, %(2) + 0.5 
   RETURN
withdraw_z:
     JZ, %(3) - 0.5 
   RETURN
withdraw_all:
     J3, %(1) + 0.5, %(2) + 0.5, %(3) - 0.5 
   RETURN

failure:
' Proximity Switch not Detected ! (Abandoning)
END

UNIT_ERROR:
CN, 91                  'Run file switching units (automatically restored at end by FabMo?)
RETURN
END
