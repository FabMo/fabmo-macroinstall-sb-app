'This routine allows you to determine the height of Z-Zero touch-off plate compared to material surface
'11/11/16, 2/2/17 th

$zero_plate_adder := .75
$current_cutter_Zoffset := 2.0

&orig_cutter_offset = $current_cutter_Zoffset
&temp_top = $current_cutter_Zoffset - 0.5
&temp_Zplate = $zero_plate_adder

'Before doing this adjustment, you need to have run Macro#3 (or Macro#2 if this is a new cutter). 
	PAUSE
    PAUSE 1

'Then you need to have carefully moved the cutter tip exactly to the surface. If you need to do that ... click QUIT.
	PAUSE 2
	ZZ

'OK, now we're ready to continue ...
	MZ, &temp_top
	M2, 6, 0

'Tool should be in position, Pull out Zeroing Plate and Put Clip on Shank of Cutter.
	PAUSE
		
'Get Values at Startup
		&startZ_speed = %(73)
		&startZ_ramp = %(83)

'Starting Settings for Measuring Z
        VS, 4, .5        
        &Z_plunge_target = -5
        &Z_pullup = .5
        &Z_final = 1.5

'HERE WE GO ! .............................................................
    'Next command creates an interrupt handler for contact with zero plate
        VS,,0.5
        VR,, 10000  ' quick stop
        ON INPUT(1,1) GOTO afterHit
        MZ, &Z_plunge_target
            GOTO FAIL
        afterHit:
            GOSUB Backoff

    'Now, let's do again with a touch at a reliable distance and speed
        VS,,.25
        VR,, 10000  ' quick stop
        ON INPUT(1,1) GOTO Finish
        MZ, &Z_plunge_target
            GOTO FAIL
        Finish:
            GOSUB Backoff

    'Set Location and Zero
		$zero_plate_adder = %(3) - &Z_pullup
        $current_cutter_Zoffset = ($zero_plate_adder - &temp_Zplate) + &orig_cutter_offset  ' remember we're pulled up here
        &new_base_Z_loc = &Z_pullup + $zero_plate_adder
        VA,,, &new_base_Z_loc,,,,,, &new_base_Z_loc

    'Restore Values
        VS, &startXY_speed, &startZ_speed
        VR, , &startZ_ramp 
        MZ, &Z_final        

    'Re-Measurement Complete. REMOVE Aligator Clip and PARK Target !! (-Resume- when Done)
        PAUSE

    '{{Eventually we'd like a choice here to move home or to park}}
        
END

'SUBROUTINES BELOW HERE ---------------------------

Backoff:
    &NextPullUp = %(3)
    &NextPullUp = &NextPullUp + &Z_pullup
    MZ, &NextPullUp
    RETURN

FAIL:
    'No Contact with Plate ... Ending!
    PAUSE
    END

Already_Triggered:
    'Z-contact seems already triggered ... Ending!
    PAUSE
    END
   
